plugins {
    id 'java'
    id 'plugin.coverage'
    id 'plugin.spotless'
    id 'plugin.sonar'

    alias(libs.plugins.kotlin)
    alias(libs.plugins.spring.boot)
    alias(libs.plugins.spring.dependency.management)
}

allprojects {
    group = 'com.swygbro'
    description = 'airoad-backend'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot Starter
    implementation libs.spring.boot.starter.web
    implementation libs.spring.boot.starter.data.jpa
    implementation libs.spring.boot.openapi.starter.webmvc.ui
    implementation libs.spring.boot.starter.websocket
    implementation libs.spring.boot.starter.actuator
    /* ───────── Spring Security ───────── */
    implementation libs.spring.boot.starter.security
    implementation libs.spring.boot.starter.oauth2.client
    /* ───────── JWT ───────── */
    implementation libs.bundles.jjwt
    implementation libs.spring.boot.starter.data.redis

    // Spring AI
    implementation platform(libs.spring.ai.bom)
    implementation libs.spring.ai.openai
    implementation libs.spring.ai.advisors.vector.store
    implementation libs.spring.ai.chat.memory.repository.jdbc

    // Hibernate
    implementation libs.hibernate.spatial

    // Redis (Lettuce)
    implementation libs.lettuce.core

    // Lombok
    compileOnly libs.lombok
    annotationProcessor libs.lombok

    // Development
    developmentOnly libs.spring.boot.starter.devtools

    // Database
    runtimeOnly libs.postgresql

    // Test
    testImplementation libs.spring.boot.starter.test
    testRuntimeOnly libs.h2
    testCompileOnly libs.lombok
    testAnnotationProcessor libs.lombok
    testImplementation libs.spring.security.test
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}

tasks.named("check") {
    dependsOn(tasks.named("jacocoTestCoverageVerification"))
    dependsOn(tasks.named("spotlessCheck"))

    doLast {
        print("커버리지 및 포맷팅 검사가 완료되었습니다:)")
    }
}
